
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКоманды.ПриСозданииНаСервере(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды

	// ++ Дроздов ____ 28.12.2025
	ПолеКонтактноеЛицо = Элементы.Добавить("КонтактноеЛицо", Тип("ПолеФормы"), Элементы.ГруппаШапкаПраво);
	ПолеКонтактноеЛицо.ПутьКДанным = "Объект.Net_КонтактноеЛицо";
	ПолеКонтактноеЛицо.Вид = ВидПоляФормы.ПолеВвода;
	ПолеКонтактноеЛицо.Заголовок = "Контактное лицо";
	
	ГруппаСкидка = Элементы.Добавить("ГруппаСкидка", Тип("ГруппаФормы"), Элементы.ГруппаШапкаЛево);
	ГруппаСкидка.Вид = ВидГруппыФормы.ОбычнаяГруппа;
	
	
	ПолеСогласованнаяСкидка = Элементы.Добавить("СогласованнаяСкидка", Тип("ПолеФормы"), ГруппаСкидка);
	ПолеСогласованнаяСкидка.ПутьКДанным = "Объект.Net_СогласованнаяСкидка";
	ПолеСогласованнаяСкидка.Вид = ВидПоляФормы.ПолеВвода;
	ПолеСогласованнаяСкидка.Заголовок = "Согласованная скидка";
	ПолеСогласованнаяСкидка.УстановитьДействие("ПриИзменении", "Net_ИзменениеСогласованнойСкидки");
	
	КомандаПересчитатьТаблицу = Команды.Добавить("ПересчитатьТаблицу");
	КомандаПересчитатьТаблицу.Заголовок = "Пересчитать таблицу";
	КомандаПересчитатьТаблицу.Картинка = БиблиотекаКартинок.Обновить;
	КомандаПересчитатьТаблицу.Отображение = ОтображениеКнопки.КартинкаИТекст;
	КомандаПересчитатьТаблицу.Действие = "Net_ПересчитатьТаблицу";
	
	КнопкаПересчитатьТаблицу = Элементы.Добавить("КнопкаПересчитатьТаблицу", Тип("КнопкаФормы"), ГруппаСкидка);
	КнопкаПересчитатьТаблицу.Вид = ВидКнопкиФормы.ОбычнаяКнопка;
	КнопкаПересчитатьТаблицу.ИмяКоманды = КомандаПересчитатьТаблицу.Имя;
	
	// -- Дроздов ____ 28.12.2025
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
    // СтандартныеПодсистемы.ПодключаемыеКоманды
    ПодключаемыеКомандыКлиент.НачатьОбновлениеКоманд(ЭтотОбъект);
    // Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
КонецПроцедуры

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)
	
    // СтандартныеПодсистемы.ПодключаемыеКоманды
    ПодключаемыеКомандыКлиентСервер.ОбновитьКоманды(ЭтотОбъект, Объект);
    // Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеЗаписи(ПараметрыЗаписи)
    ПодключаемыеКомандыКлиент.ПослеЗаписи(ЭтотОбъект, Объект, ПараметрыЗаписи);
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыТовары

&НаКлиенте
Процедура ТоварыКоличествоПриИзменении(Элемент)
	
	ТекущиеДанные = Элементы.Товары.ТекущиеДанные;
	
	РассчитатьСуммуСтроки(ТекущиеДанные);
	
КонецПроцедуры

&НаКлиенте
Процедура ТоварыЦенаПриИзменении(Элемент)
	
	ТекущиеДанные = Элементы.Товары.ТекущиеДанные;
	
	РассчитатьСуммуСтроки(ТекущиеДанные);
	
КонецПроцедуры

&НаКлиенте
Процедура ТоварыПриИзменении(Элемент)
	РассчитатьСуммуДокумента();
КонецПроцедуры

&НаКлиенте
Процедура ТоварыСкидкаПриИзменении(Элемент)
	
	ТекущиеДанные = Элементы.Товары.ТекущиеДанные;
	
	РассчитатьСуммуСтроки(ТекущиеДанные);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыУслуги

&НаКлиенте
Процедура УслугиКоличествоПриИзменении(Элемент)
	
	ТекущиеДанные = Элементы.Услуги.ТекущиеДанные;
	
	РассчитатьСуммуСтроки(ТекущиеДанные);
	
КонецПроцедуры

&НаКлиенте
Процедура УслугиЦенаПриИзменении(Элемент)
	
	ТекущиеДанные = Элементы.Услуги.ТекущиеДанные;
	
	РассчитатьСуммуСтроки(ТекущиеДанные);
	
КонецПроцедуры

&НаКлиенте
Процедура УслугиСкидкаПриИзменении(Элемент)
	
	ТекущиеДанные = Элементы.Услуги.ТекущиеДанные;
	
	РассчитатьСуммуСтроки(ТекущиеДанные);
	
КонецПроцедуры

&НаКлиенте
Процедура УслугиПриИзменении(Элемент)
	РассчитатьСуммуДокумента();
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура РассчитатьСуммуСтроки(ТекущиеДанные)
	
	// ++ Дроздов ____ 28.12.2015
	
	//КоэффициентСкидки = 1 - ТекущиеДанные.Скидка / 100;
	//ТекущиеДанные.Сумма = ТекущиеДанные.Цена * ТекущиеДанные.Количество * КоэффициентСкидки;
	
	СуммаБезСкидки = ТекущиеДанные.Цена * ТекущиеДанные.Количество;
	СкидкаСуммированная = ТекущиеДанные.Скидка + Объект.Net_СогласованнаяСкидка;
	
	Если СкидкаСуммированная > 100 Тогда
		ОбщегоНазначенияКлиент.СообщитьПользователю(СтрШаблон("Суммарная скидка для %1 превышает 100%%, сумма была сброшена", ТекущиеДанные.Номенклатура));
		ТекущиеДанные.Сумма = 0;
		Возврат;
	КонецЕсли;
	
	ТекущиеДанные.Сумма = СуммаБезСкидки - СуммаБезСкидки * (СкидкаСуммированная / 100);
	РассчитатьСуммуДокумента();
	// -- Дроздов ____ 28.12.2015 

КонецПроцедуры

&НаКлиенте
Процедура РассчитатьСуммуДокумента()
	
	Объект.СуммаДокумента = Объект.Товары.Итог("Сумма") + Объект.Услуги.Итог("Сумма");
	
КонецПроцедуры

&НаКлиенте
Процедура Net_ПересчитатьТаблицу(Команда)
	// ++ Дроздов ____ 28.12.2015 
	Net_РассчитатьТабличнуюЧастьСоСкидкой(Объект.Товары);
	Net_РассчитатьТабличнуюЧастьСоСкидкой(Объект.Услуги);
	// -- Дроздов ____ 28.12.2015
КонецПроцедуры

&НаКлиенте
Процедура Net_РассчитатьТабличнуюЧастьСоСкидкой(ТабличнаяЧасть)
	// ++ Дроздов ____ 28.12.2015
	Для каждого ЭлементТЧ Из ТабличнаяЧасть Цикл
		
		РассчитатьСуммуСтроки(ЭлементТЧ);
		
	КонецЦикла;
	// -- Дроздов ____ 28.12.2015
КонецПроцедуры

&НаКлиенте
Процедура Net_ИзменениеСогласованнойСкидки()
	// ++ Дроздов ____ 28.12.2015
	Если ЗначениеЗаполнено(Объект.Товары) Или ЗначениеЗаполнено(Объект.Услуги) Тогда
		
		Net_ЗадатьВопросАсинх();
		
	КонецЕсли;
	// -- Дроздов ____ 28.12.2015
КонецПроцедуры

&НаКлиенте
Асинх Процедура Net_ЗадатьВопросАсинх()
	// ++ Дроздов ____ 28.12.2015
	Ответ = Ждать ВопросАсинх("Изменен процент скидки. Пересчитать таблицу товаров и услуг?", РежимДиалогаВопрос.ДаНет,, КодВозвратаДиалога.Да);
	Если Ответ = КодВозвратаДиалога.Да Тогда
		Net_РассчитатьТабличнуюЧастьСоСкидкой(Объект.Товары);
		Net_РассчитатьТабличнуюЧастьСоСкидкой(Объект.Услуги);
	КонецЕсли;
	// -- Дроздов ____ 28.12.2015
КонецПроцедуры

#Область ПодключаемыеКоманды

// СтандартныеПодсистемы.ПодключаемыеКоманды
&НаКлиенте
Процедура Подключаемый_ВыполнитьКоманду(Команда)
    ПодключаемыеКомандыКлиент.НачатьВыполнениеКоманды(ЭтотОбъект, Команда, Объект);
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ПродолжитьВыполнениеКомандыНаСервере(ПараметрыВыполнения, ДополнительныеПараметры) Экспорт
    ВыполнитьКомандуНаСервере(ПараметрыВыполнения);
КонецПроцедуры

&НаСервере
Процедура ВыполнитьКомандуНаСервере(ПараметрыВыполнения)
    ПодключаемыеКоманды.ВыполнитьКоманду(ЭтотОбъект, ПараметрыВыполнения, Объект);
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ОбновитьКоманды()
    ПодключаемыеКомандыКлиентСервер.ОбновитьКоманды(ЭтотОбъект, Объект);
КонецПроцедуры
// Конец СтандартныеПодсистемы.ПодключаемыеКоманды

#КонецОбласти

#КонецОбласти
